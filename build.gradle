plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id "com.ly.smart-doc" version "3.0.1"
}

group = 'com.synway.vpay'
version = '0.0.1-SNAPSHOT'

java {
    targetCompatibility = '17'
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

smartdoc {
    configFile = file("src/main/resources/smart-doc.json")
}

bootRun {
    sourceResources sourceSets.main
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation("org.mindrot:jbcrypt:0.4")
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.register('copyTemplates', Copy) {
    // 清除现有的配置目录
    delete fileTree(dir: 'build/libs/templates')

    // 复制模板文件到libs/templates目录下
    from('templates')
    into 'build/libs/templates'
}

tasks.register('copyDependencies', Copy) {
    // 删除libs/lib文件夹下的jar包
    delete fileTree(dir: 'build/libs/lib', include: '*.jar')

    // 复制依赖jar包到libs/lib目录下
    from configurations.runtimeClasspath
    into 'build/libs/lib'
    include '*.jar'
}

bootJar {
    excludes = ['*.jar']

    // 外置依赖包、模板文件
    dependsOn(copyDependencies, copyTemplates)

    // 修改MANIFEST.MF文件，指定类路径
    manifest {
        attributes "Manifest-Version": 1.0,
                'Class-Path': configurations.runtimeClasspath.files.collect { "lib/" + it.getName() }.join(' ')
    }
}
